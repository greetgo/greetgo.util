apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'maven'
apply plugin: 'idea'

group = 'kz.greetgo'

sourceCompatibility = 1.7
version = '0.0.27'

def nowDate = new Date()
def nowFormattedDate = nowDate.format("yyyy-MM-dd/HH:mm:ss")

eclipse.project.natures 'org.springsource.ide.eclipse.gradle.core.nature'

jar {
  manifest {
    attributes 'Implementation-Title': project.name,
      'Implementation-Version': version, 'Implementation-Vendor': 'greetgo!',
      'Built-By': System.getProperty("user.name"), 'Built-At': nowFormattedDate
  }
}

sourceSets {
  main {
    java.srcDirs = ["src", "src_resources"]
    resources.srcDirs = ["src", "src_resources"]
  }
  test {
    java.srcDirs = ["test_src", "test_src_resources"]
    resources.srcDirs = ["test_src", "test_src_resources"]
  }
}

eclipse {
  jdt {
    //
    // if you want to alter the java versions (by default they are configured with
    // gradle java plugin settings):
    //
    sourceCompatibility = 1.7
    targetCompatibility = 1.7
  }

}

project.eclipse.classpath.file.whenMerged { cp ->
  if (project.hasProperty("sourceCompatibility")) {
    cp.entries.findAll { it.path.contains('JRE_CONTAINER') }.each {

      it.path += "/org.eclipse.jdt.internal.debug.ui.launcher.StandardVMType/" +
          "JavaSE-$project.sourceCompatibility"
    }
  }
}

repositories {
  mavenLocal()
  if (System.getenv('GG_REPO') != null && System.getenv('GG_REPO').length() > 0) {
    maven { url System.getenv('GG_REPO') }
  }
  mavenCentral()
}

dependencies {
  compile 'com.google.code.findbugs:annotations:2.0.1'
  
  testCompile 'org.testng:testng:6.5.1'
  testCompile 'org.easytesting:fest-assert-core:2.0M10'

  testCompile "kz.greetgo:greetgo.conf:0.0.17"

  testCompile 'postgresql:postgresql:9.1-901.jdbc4'
  testCompile 'c3p0:c3p0:0.9.1.2'

  testCompile "greetgo.3rd-party:oracle-ojdbc6_g:11.2.0.3"
  testCompile "greetgo.3rd-party:oracle-ucp:11.2.0.3"

  testCompile 'hsqldb:hsqldb:1.8.0.10'
  testCompile 'mysql:mysql-connector-java:5.1.38'
  
}

test { useTestNG() }

task javadocJar(type: Jar, dependsOn: 'javadoc') {
  from javadoc.destinationDir
  classifier = 'javadoc'
}

task sourcesJar(type: Jar) {
  from sourceSets.main.allSource
  classifier = 'sources'
}

artifacts {
  archives javadocJar
  archives sourcesJar
}

uploadArchives {
  repositories.mavenDeployer {
    configuration = configurations.archives
    repository(url: System.getenv('GG_REPO'))

    pom.project {
      name project.name
      packaging 'jar'
    }
  }
}
