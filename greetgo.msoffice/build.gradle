apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'maven'

group = 'kz.greetgo'

sourceCompatibility = 1.7
version = '0.2.3'

def nowDate = new Date()
def nowFormattedDate = nowDate.format("yyyy-MM-dd/HH:mm:ss")

jar {
  manifest {
    attributes 'Implementation-Title': project.name, 
      'Implementation-Version': version, 'Implementation-Vendor': 'greetgo!',
      'Built-By': System.getProperty("user.name"), 'Built-At': nowFormattedDate
  }
}

eclipse {
  jdt {
    //
    // if you want to alter the java versions (by default they are configured with
    // gradle java plugin settings):
    //
    sourceCompatibility = 1.7
    targetCompatibility = 1.7
  }
  
  pathVariables 'GRADLE_USER_HOME': gradle.gradleUserHomeDir
}

project.eclipse.classpath.file.whenMerged { cp ->
  if (project.hasProperty("sourceCompatibility")) {
    cp.entries.findAll { it.path.contains('JRE_CONTAINER') }.each {
      
      it.path += "/org.eclipse.jdt.internal.debug.ui.launcher.StandardVMType/" +
          "JavaSE-$project.sourceCompatibility"
    }
  }
}

import org.gradle.plugins.ide.eclipse.model.SourceFolder as SF

eclipse.classpath.file.whenMerged { cp ->
  cp.entries.findAll { it instanceof SF }*.output = "build/eclipse_bin"
}

repositories {
  mavenRepo (
    name: 'Our repository',
    url: 'http://192.168.0.1:8080/repository/internal/'
  )
  mavenCentral()
}

configurations { repoUpload }

dependencies {
  testCompile 'org.testng:testng:6.1.1'
  testCompile 'org.easytesting:fest-assert-core:2.0M10'
  
  // needed for DAV protocol on maven uploads
  repoUpload "org.apache.maven.wagon:wagon-webdav-jackrabbit:1.0-beta-6"
}

test { useTestNG() }

task javadocJar(type: Jar, dependsOn: 'javadoc') {
  from javadoc.destinationDir
  classifier = 'javadoc'
}

task sourcesJar(type: Jar) {
  from sourceSets.main.allSource
  classifier = 'sources'
}

artifacts {
  archives javadocJar
  archives sourcesJar
}

sourceSets {
  main {
    resources.srcDirs = ["src/main/java", "src/main/resources"]
  }
}

uploadArchives {
  repositories.mavenDeployer {
    configuration = configurations.archives
    repository(
      url: 'http://192.168.0.1:8080/repository/internal/'
    )
    
    pom.project {
      name project.name
      packaging 'jar'
    }
  }
}
